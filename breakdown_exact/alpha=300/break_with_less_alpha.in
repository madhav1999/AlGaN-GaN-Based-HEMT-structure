go atlas

set vstart = 0
set vstop = 15
set vinc = .5
set light=1e-5

mesh width=100 ^diag.flip

x.m l=0 s=0.2
x.m l=0.5 s=0.2
x.m l=1.0 s=0.1
x.m l=3 s=0.25
x.m l=4 s=0.25
x.m l=6 s=0.1
x.m l=9 s=0.75

y.m l=-5 s=1
y.m l=0.2 s=0.2
y.m l=0.5 s=0.01
y.m l=0.5267 s=0.0025
y.m l=0.75 s=0.1
y.m l=1.0 s=0.05
y.m l=2.0 s=0.25
y.m l=3.0 s=3
y.m l=20 s=3

region num=1 x.min=0.5 x.max=8.5 y.max=0.5267 mat=AlGaN x.comp=0.25
region num=2 x.min=0.0 x.max=9.0 y.min=-5 y.max=0.5 mat=nitride insulator
region num=3 x.min=0.5 x.max=8.5 y.min=0.5267 y.max=1 mat=GaN donors=1e15 substrate
region num=4 x.min=0.0 x.max=9.0 y.min=1.0 y.max=2.0 mat=GaN
region num=5 x.min=0.0 x.max=9.0 y.min=2.0 y.max=20.0 mat=sapphire insulator

elec num=1 name=source x.min=0 x.max=1 y.min=-5 y.max=1
elec num=2 name=drain x.min=6 x.max=9 y.min=-5 y.max=1
elec num=3 name=gate x.min=3 x.max=4 y.min=0.2 y.max=0.5
elec num=4 substrate

contact name=gate work=5.1
contact name=source work=4.31
contact name=drain work=4.31

models k.p print srh
model lat.temp
mobility FMCT.N Gansat.N
model polarization calc.strain polar.scale=0.75
model region=1 pch.elec
interface neutralize x.min=3 x.max=4 y.min=0.48 y.max=0.52
thermcontact num=1 y.min=20 y.max=20 ext.temp=300 alpha=300

output con.band val.band charge polar.charge band.par e.mobility

method newton trap maxtraps=25



############################################################################## 
# idvg curve
##############################################################################
solve

log outf=break_with_diff_alpha02_0.log
solve vdrain=0.05
solve vstep=-0.2 vfinal=-2 name=gate 
solve vstep=-0.1 vfinal=-4 name=gate
log off

save outfile=break_with_diff_alpha02_0.str

extract init infile="break_with_diff_alpha02_0.log"
extract name="Vpinchoff" xintercept(maxslope(curve(v."gate",i."drain")))

############################################################################## 
# idvd breakdown curves
##############################################################################
method autonr gcarr.itlimit=10 clim.dd=1e3 clim.eb=1e3 nblockit=25
solve init 
# turn on optical source to help initiate breakdown
solve b1=$light index.check
#
solve nsteps=10 vfinal=$Vpinchoff name=gate b1=$light

log outf=break_with_diff_alpha02_1.log

solve  vstep=0.1 vfinal=1  name=drain b1=$light
solve  vstep=1   vfinal=10 name=drain b1=$light
solve  vstep=2   vfinal=20 name=drain b1=$light
solve  vstep=5 vfinal=1200 name=drain  b1=$light cname=drain compl=0.5

# change to current contact to resolve breakdown*
contact name=drain current
solve 
solve imult istep=1.1 ifinal=1 name=drain
#
save outfile=break_with_diff_alpha02_1.str 
#
tonyplot break_with_diff_alpha02_1.log

quit
